start -> production (nl production)* | ε

symbol -> [[:alnum:]_] [-_[:alnum:]]*
ws -> [[:blank:]]*
wsnl -> [[:space:]]*
nl -> '\n' '\n'*

production -> ws symbol wsnl '-' '>' wsnl nonterminal ws

nonterminal -> alternation
alternation -> concatenation (wsnl '|' wsnl concatenation)*
concatenation -> repetition (wsnl repetition)*
repetition -> (symbol | terminal | '(' wsnl nonterminal wsnl ')') wsnl ('*' | ε)

terminal -> literal | character-set | any

literal -> '\'' literal-character '\''
literal-character -> '\\' (escaped-character | '\'') | [^']

character-set -> '[' ('^' | ε) character-set-character character-set-character* ']'
character-set-character -> ('[' ':' ('alnum' | 'alpha' | 'blank' | 'digit' | 'space' | '') ':' ']') | '\\' (escaped-character | ']') | [^\]]

escaped-character -> '\\' | 'n' | 'r' | 't'

any -> '.'
